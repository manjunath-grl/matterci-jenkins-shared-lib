@Library('matter-shared-lib') _

pipeline {
    agent none

    options {
        timestamps()
        disableConcurrentBuilds()
    }

    parameters {
        string(name: 'CTRL_BRANCH', defaultValue: 'v2.15-beta1+winter2025', description: 'Override CTRL SDK branch')
        string(name: 'DUT_BRANCH',  defaultValue: 'v1.5-branch', description: 'Override DUT SDK branch')
        string(name: 'Config_Path', defaultValue: '/Users/mac-mini3/ci_config.yaml', description: 'Optional absolute path to ci_config.yaml')
    }

    stages {

        stage('Load CI Config') {
            agent { label 'Chip_Mac3' }
            steps {
                script {
                    def configPath = params.Config_Path?.trim()
                            ? params.Config_Path
                            : 'resources/ci_config.yaml'

                    def ciConfig = loadCiConfig(configPath)

                    /* Override via Jenkins parameters */
                    if (params.CTRL_BRANCH?.trim()) {
                        ciConfig.ci_config.ctrl_sdk.branch = params.CTRL_BRANCH
                    }
                    if (params.DUT_BRANCH?.trim()) {
                        ciConfig.ci_config.dut_sdk.branch = params.DUT_BRANCH
                    }

                    /* Make available to later stages */
                    env.CI_CONFIG_JSON = groovy.json.JsonOutput.toJson(ciConfig)
                }
            }
        }

        stage('Resolve Build Strategy') {
            agent { label 'local_mac_node' }
            steps {
                script {
                    def ciConfig = readJSON text: env.CI_CONFIG_JSON
                    steps.echo("CI Config: ${ciConfig}")
                    def buildStrategy = resolveBuildStrategy(ciConfig)
                    steps.echo("Build Strategy: ${buildStrategy}")

                    env.BUILD_STRATEGY_JSON =
                            groovy.json.JsonOutput.toJson(buildStrategy)
                }
            }
        }

        stage('Clone CTRL SDK') {
            agent { label "${readJSON(text: env.CI_CONFIG_JSON).ci_config.raspi_controller_node}" }
            steps {
                script {
                    def ciConfig = readJSON text: env.CI_CONFIG_JSON
                    cloneSdk(ciConfig.ci_config.ctrl_sdk, "ctrl_sdk")
                }
            }
        }

        stage('Clone DUT SDK') {
            agent { label "${readJSON(text: env.CI_CONFIG_JSON).ci_config.build_config.dut.node}" }
            steps {
                script {
                    def ciConfig = readJSON text: env.CI_CONFIG_JSON
                    cloneSdk(ciConfig.ci_config.dut_sdk, "dut_sdk")
                }
            }
        }

        stage('BUILD ARTIFACTS') {
            parallel {
                stage('Build CTRL') {
                    when {
                        expression {
                            readJSON(text: env.BUILD_STRATEGY_JSON).ctrl.build
                        }
                    }
                    agent {
                        label readJSON(text: env.CI_CONFIG_JSON).ci_config.raspi_controller_node
                    }
                    steps {
                        script {
                            def ciConfig = readJSON text: env.CI_CONFIG_JSON
                            buildCtrl(ciConfig)
                            archiveArtifactsEx("CTRL")
                        }
                    }
                }

                stage('Build DUT (Docker)') {
                    when {
                        expression {
                            readJSON(text: env.BUILD_STRATEGY_JSON).dut.build
                        }
                    }
                    agent {
                        label readJSON(text: env.CI_CONFIG_JSON).ci_config.build_config.dut.node
                    }
                    steps {
                        script {
                            def ciConfig = readJSON text: env.CI_CONFIG_JSON
                            buildDutDocker(ciConfig)
                            archiveArtifactsEx("DUT")
                        }
                    }
                }

            }
        }

        stage('Reuse CTRL Artifacts') {
            when {
                expression {
                    !readJSON(text: env.BUILD_STRATEGY_JSON).ctrl.build
                }
            }
            steps {
                script {
                    def ciConfig = readJSON text: env.CI_CONFIG_JSON
                    copyArtifactsEx("CTRL", ciConfig)
                }
            }
        }

        stage('Reuse DUT Artifacts') {
            when {
                expression {
                    !readJSON(text: env.BUILD_STRATEGY_JSON).dut.build
                }
            }
            steps {
                script {
                    def ciConfig = readJSON text: env.CI_CONFIG_JSON
                    copyArtifactsEx("DUT", ciConfig)
                }
            }
        }

        stage('Install Binaries') {
            parallel {
                stage('Install CTRL') {
                    agent { label "${readJSON(text: env.CI_CONFIG_JSON).ci_config.raspi_controller_node}" }
                    steps {
                        installBinaries("CTRL")
                    }
                }
                stage('Install DUT') {
                    agent { label "${readJSON(text: env.CI_CONFIG_JSON).ci_config.raspi_device_node}" }
                    steps {
                        installBinaries("DUT")
                    }
                }
            }
        }

        stage('Run Certification Tests') {
            when {
                expression {
                    readJSON(text: env.CI_CONFIG_JSON).ci_config.test_run_config.enabled
                }
            }
            agent { label "${readJSON(text: env.CI_CONFIG_JSON).ci_config.raspi_controller_node}" }
            steps {
                script {
                    def ciConfig = readJSON text: env.CI_CONFIG_JSON
                    runTests(ciConfig.ci_config.test_run_config)
                }
            }
        }
    }

    post {
        failure {
            echo "❌ Backward compatibility pipeline FAILED"
        }
        success {
            echo "✅ Backward compatibility pipeline completed SUCCESSFULLY"
        }
    }
}
